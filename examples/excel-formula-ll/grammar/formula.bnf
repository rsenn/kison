formula ::= equalExp
equalExp ::= addExp
equalExp ::= equalExp '=' addExp
equalExp ::= equalExp '<=' addExp
equalExp ::= equalExp '>=' addExp
equalExp ::= equalExp '<>' addExp
equalExp ::= equalExp '>' addExp
equalExp ::= equalExp '<' addExp
addExp ::= mulExp
addExp ::= addExp '+' mulExp
addExp ::= addExp '-' mulExp
mulExp ::= expoExp
mulExp ::= mulExp '*' expoExp
mulExp ::= mulExp '/' expoExp
expoExp ::= concatExp
expoExp ::= expoExp '^' concatExp
concatExp ::= percentExp
concatExp ::= concatExp '&' percentExp
percentExp ::= percentExp '%'
percentExp ::= prefixExp
prefixExp ::= '-' prefixExp
prefixExp ::= '+' prefixExp
prefixExp ::= '@' prefixExp
prefixExp ::= atomExp
atomExp ::= '(' equalExp ')'
atomExp ::= 'NUMBER'
atomExp ::= 'STRING'
atomExp ::= 'LOGIC'
atomExp ::= 'ERROR'
atomExp ::= reference
referenceItem ::= 'CELL'
referenceItem ::= 'NAME'
referenceItem ::= structureReference
reference ::= unionReference
unionReference ::= unionReference 'REF_UNION_OPERATOR' intersectReference
unionReference ::= intersectReference
intersectReference ::= intersectReference expandReference
intersectReference ::= expandReference
expandReference ::= expandReference 'REF_EXPAND_OPERATOR' referenceItem
expandReference ::= referenceItem
atomExp ::= function
atomExp ::= array
arrayElement ::= 'STRING'
arrayElement ::= 'NUMBER'
arrayElement ::= 'LOGIC'
arrayElement ::= 'ERROR'
arrayList ::= arrayElement
arrayList ::= arrayList 'ARRAY_SEPARATOR' arrayElement
array ::= '{' arrayList '}'
function ::= 'FUNCTION' '(' arguments ')'
argument ::=
argument ::= equalExp
arguments ::= argument
arguments ::= arguments 'ARGUMENT_SEPARATOR' argument
structureReference ::= 'TABLE_NAME' tableSpecifier
structureReference ::= tableSpecifier
tableSpecifier ::= 'TABLE_ITEM_SPECIFIER'
tableSpecifier ::= '[' tableSpecifierInner ']'
tableThisRow ::= 'TABLE_@'
tableThisRow ::= 'TABLE_@' 'TABLE_COLUMN_SPECIFIER'
tableSpecifierInner ::= tableThisRow
tableSpecifierInner ::= tableColumnSpecifier
tableSpecifierItem ::= 'TABLE_COLUMN_SPECIFIER'
tableSpecifierItem ::= 'TABLE_ITEM_SPECIFIER'
tableColumnSpecifier ::= tableSpecifierItem
tableColumnSpecifier ::= tableColumnSpecifier 'SPECIFIER_SEPARATOR' tableSpecifierItem